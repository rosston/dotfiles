" Use vim settings, rather than vi setting
" This must be first, because it changes other options as a side effect.
set nocompatible

scriptencoding utf-8
set encoding=utf-8

" Use vim-plug to manage plugins
filetype off                    " force reloading *after* vim-plug loads
call plug#begin('~/.vim/plugged')

function! DoRemote(arg)
  UpdateRemotePlugins
endfunction

Plug 'mileszs/ack.vim'
Plug 'w0rp/ale'
Plug 'bkad/CamelCaseMotion'
Plug 'GertjanReynaert/cobalt2-vim-theme'
Plug 'ctrlpvim/ctrlp.vim', { 'on': [] }
Plug 'Shougo/deoplete.nvim', { 'do': function('DoRemote'), 'on': [] }
Plug 'mattn/emmet-vim', { 'for': 'html' }
Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': './install --all', 'on': [] }
Plug 'junegunn/fzf.vim', { 'on': [] }
Plug 'Shougo/neocomplete.vim', { 'on': [] }
Plug 'mhartington/nvim-typescript', { 'do': './install.sh' }
Plug 'kovisoft/paredit', { 'on': [] }
Plug 'junegunn/rainbow_parentheses.vim'
Plug 'rust-lang/rust.vim'
Plug 'leafgarland/typescript-vim'
Plug 'SirVer/ultisnips'
Plug 'tpope/vim-abolish'
Plug 'MarcWeber/vim-addon-local-vimrc'
Plug 'vim-airline/vim-airline'
Plug 'vim-airline/vim-airline-themes'
Plug 'moll/vim-bbye'
Plug 'tpope/vim-capslock'
Plug 'guns/vim-clojure-static'
Plug 'kchmck/vim-coffee-script'
Plug 'tpope/vim-commentary'
Plug 'tommcdo/vim-exchange'
Plug 'tpope/vim-fireplace', { 'for': 'clojure' }
Plug 'flowtype/vim-flow', { 'for': 'javascript' }
Plug 'tpope/vim-fugitive'
Plug 'tpope/vim-git'
Plug 'airblade/vim-gitgutter'
Plug 'fatih/vim-go'
Plug 'jparise/vim-graphql'
Plug 'nathanaelkane/vim-indent-guides'
Plug 'pangloss/vim-javascript', { 'for': 'javascript' }
Plug 'MaxMEllon/vim-jsx-pretty', { 'for': 'javascript' }
Plug 'peitalin/vim-jsx-typescript'
Plug 'lfe-support/vim-lfe'
Plug 'tpope/vim-obsession'
Plug 'dbakker/vim-projectroot'
Plug 'digitaltoad/vim-pug'
Plug 'google/vim-searchindex'
Plug 'justinmk/vim-sneak'
Plug 'honza/vim-snippets'
Plug 'tpope/vim-surround'
Plug 'hashivim/vim-terraform'
Plug 'thinca/vim-textobj-between'
Plug 'nelstrom/vim-textobj-rubyblock'
Plug 'kana/vim-textobj-user'
Plug 'tpope/vim-unimpaired'

call plug#end()

" Set up an augroup for everything to use
augroup vimrc
  au!
augroup END

" Custom load some plugins
if has('nvim') " Neovim only
    call plug#load('deoplete.nvim')
    call deoplete#enable()
    call deoplete#custom#option({
    \ 'auto_complete_delay': 150,
    \ 'ignore_case': v:true,
    \ 'smart_case': v:true,
    \ })
else " Vim only (not Neovim)
    call plug#load('neocomplete.vim')
    autocmd vimrc InsertEnter * NeoCompleteEnable
endif
if has('gui_running')
    call plug#load('ctrlp.vim')
else
    call plug#load('fzf', 'fzf.vim')
endif

filetype plugin indent on       " enable detection, plugins and indenting in one step

" Turn on syntax highlighting
syntax on

" Turn on omni completion
set omnifunc=syntaxcomplete#Complete

" Set space as mapleader
let mapleader="\<Space>"

" Editing behaviour {{{
set showmode                    " always show what mode we're currently editing in
set tabstop=2                   " a tab is four spaces
set softtabstop=2               " when hitting <BS>, pretend like a tab is removed, even if spaces
set expandtab                   " expand tabs by default (overloadable per file type later)
set shiftwidth=2                " number of spaces to use for autoindenting
set shiftround                  " use multiple of shiftwidth when indenting with '<' and '>'
set backspace=indent,eol,start  " allow backspacing over everything in insert mode
set autoindent                  " always set autoindenting on
set copyindent                  " copy the previous indentation on autoindenting
set number                      " always show line numbers
set showmatch                   " set show matching parenthesis
runtime macros/matchit.vim      " make % match opening/closing HTML tags
set ignorecase                  " ignore case when searching
set smartcase                   " ignore case if search pattern is all lowercase,
                                "    case-sensitive otherwise
set smarttab                    " insert tabs on the start of a line according to
                                "    shiftwidth, not tabstop
set scrolloff=20                " keep 4 lines off the edges of the screen when scrolling
set hlsearch                    " highlight search terms
set incsearch                   " show search matches as you type
set gdefault                    " search/replace "globally" (on a line) by default

" set invisible characters
set listchars=tab:▸\ ,trail:·,extends:#,nbsp:·

set nolist                      " don't show invisible characters by default,
                                " but it is enabled for some file types (see later)
"set pastetoggle=<F2>            " when in insert mode, press <F2> to go to
                                "    paste mode, where you can paste mass data
                                "    that won't be autoindented
set mouse=a                     " enable using the mouse if terminal emulator
                                "    supports it (xterm does)

set fileformats="unix,dos,mac"
set wrap
set textwidth=79

set formatoptions=qrn1j

set colorcolumn=+1

set nrformats=                  " make <C-a> and <C-x> play well with
                                "    zero-padded numbers (i.e. don't consider
                                "    them octal or hex)

set shortmess+=I                " hide the launch screen
set clipboard=unnamedplus       " normal OS clipboard interaction
set autoread                    " automatically reload files changed outside of Vim
set ttimeoutlen=50
" }}}


" Editor layout {{{
set termencoding=utf-8
set encoding=utf-8
set lazyredraw                  " don't update the display while executing macros
set laststatus=2                " tell VIM to always put a status line in, even
                                "    if there is only one window
set cmdheight=2                 " use a status bar that is 2 rows high

set ruler                       " enable line/col numbers in status bar
" }}}


" Vim behaviour {{{
set hidden                      " hide buffers instead of closing them this
                                "    means that the current buffer can be put
                                "    to background without being written; and
                                "    that marks and undo history are preserved
set switchbuf=useopen           " reveal already opened files from the
                                " quickfix window instead of opening new
                                " buffers
set history=1000                " remember more commands and search history
set undolevels=1000             " use many muchos levels of undo
set undofile                " keep a persistent backup file
set undodir=~/.vim/tmp/.undo,~/tmp,/tmp
                                " store undo files in one of these directories
set backupcopy=yes
set backupdir=~/.vim/tmp/backup/,~/tmp,/tmp
                                " store backup files in one of these
                                " directories
set directory=~/.vim/tmp/swap/,~/tmp,/tmp
                                " store swap files in one of these directories
set wildmenu                    " make tab completion for files/buffers act like bash
set wildmode=list:full          " show a list when pressing tab and complete
                                "    first full match
set wildignore=*.swp,*.bak,*.pyc,*.class
set title                       " change the terminal's title
set visualbell                  " don't beep
set noerrorbells                " don't beep
set showcmd                     " show (partial) command in the last line of the screen
                                "    this also shows visual selection info
set nomodeline                  " disable mode lines (security measure)
set cursorline                  " underline the current line, for quick orientation

" save and restore cursor position when switching buffers
au BufLeave * let b:winview = winsaveview()
au BufEnter * if(exists('b:winview')) | call winrestview(b:winview) | endif

if has('nvim')
    set inccommand=split
end
" }}}


" Shortcut mappings {{{
source ~/.vim/visual-at.vim

" Fix Vim’s horribly broken default regex “handling” by automatically
" inserting a \v before any string you search for.
nnoremap / /\v
vnoremap / /\v

" Don't require the Shift key to form chords to enter ex mode.
nnoremap ; :
" Remap old behavior of ; (repeat last f, F, t, or T command) to <leader>;
map <leader>; <Plug>Sneak_;

" Remap j and k to act as expected when used on long, wrapped, lines
nnoremap j gj
nnoremap k gk

" Remap Y to act like C and D
noremap Y y$

" Allow repeated indent shifts
vnoremap < <gv
vnoremap > >gv

" Allow quickly searching for the visually selected text
vnoremap // y/<C-R>"<CR>

nnoremap <leader>w <C-w>

" Clears the search register
nnoremap <silent> <leader>sc :nohlsearch<CR>

" Toggle relative line numbers, makes it easier to use motion commands
nnoremap <silent> <leader>rel :set relativenumber!<cr>

" Launch Ack
nnoremap <leader>sap :ProjectRootExe Ack! ""<left>

" Copy path to current file
nnoremap <leader>yp :CopyPath<CR>

" Insert em dash
nnoremap <leader>i- :EmDash<CR>

" git shortcuts
nnoremap <leader>gs :Gstatus<CR>
nnoremap <leader>gb :Git blame<CR>

" jump to buffer by number
nnoremap <silent> <leader>bn :exe "buf" nr2char(getchar())<cr>

" }}}


" Neovim-specific mappings {{{
if has('nvim')
    :tnoremap <leader><Esc> <C-\><C-n>
endif
" }}}


" filetype-specific settings {{{
" Don't treat '-' as a word break character in the CSS language family
autocmd vimrc FileType css,less,sass,scss setlocal iskeyword+=-

" Force .lfe files to be treated as lisp
autocmd vimrc BufNewFile,BufRead *.lfe set ft=lisp
" }}}


" Strip trailing whitespace {{{
if !exists("g:rosston_strip_trailing_whitespace")
  let g:rosston_strip_trailing_whitespace=1
endif
autocmd vimrc BufWritePre * :call <SID>StripTrailingWhitespaces()
" }}}

" Style/theme settings {{{
" Enable true color in the terminal.
" For Neovim 0.1.3 and 0.1.4 < https://github.com/neovim/neovim/pull/2198 >
if (has("nvim"))
    let $NVIM_TUI_ENABLE_TRUE_COLOR=1
endif
" For Neovim > 0.1.5 and Vim > patch 7.4.1799
" < https://github.com/vim/vim/commit/61be73bb0f965a895bfb064ea3e55476ac175162 >
" Based on Vim patch 7.4.1770 (`guicolors` option)
" < https://github.com/vim/vim/commit/8a633e3427b47286869aa4b96f2bfc1fe65b25cd >
" < https://github.com/neovim/neovim/wiki/Following-HEAD#20160511 >
if (has("termguicolors"))
    set termguicolors
endif

if has('gui_running') && exists('&guifont')
  set guifont=Source\ Code\ Pro:h16
endif

colorscheme cobalt2

if g:colors_name == 'cobalt2'
    " set a sensible background color for current line
    :hi CursorLine guifg=NONE guibg=#1f4662 ctermfg=NONE

    " make the wrap guide better than pure black
    highlight ColorColumn ctermbg=lightgrey guibg=#3b5364
endif

" change cursor shape in iTerm when using Vim
if !has('nvim')
    let &t_SI = "\<Esc>]50;CursorShape=1\x7"
    let &t_EI = "\<Esc>]50;CursorShape=0\x7"
endif
" Or just let Neovim figure out the cursor shape
" }}}


" Make netrw nice {{{
" Change directory to the current buffer when opening files.
set autochdir
let g:netrw_liststyle = 3
" }}}


" vim-surround settings {{{
" Make 'o' work to surround text with nothing. Then use 'S' in vim-surround to
" wrap the text in new lines.
let b:surround_111 = "\r"
" }}}


" CamelCaseMotion settings {{{
call camelcasemotion#CreateMotionMappings('\')
" }}}


" vim-indent-guides settings {{{
let g:indent_guides_guide_size = 1
let g:indent_guides_auto_colors = 0
autocmd vimrc VimEnter,Colorscheme * :hi IndentGuidesOdd  guibg=#3b5364 ctermbg=3
autocmd vimrc VimEnter,Colorscheme * :hi IndentGuidesEven guibg=#3b5364 ctermbg=4
" }}}


" CtrlP settings {{{
let g:ctrlp_max_files = 20000
let g:ctrlp_open_multiple_files = 'i'
let g:ctrlp_show_hidden = 1
let g:ctrlp_custom_ignore = {
    \ 'file': '\v[\/](\.DS_Store)$',
    \ 'dir': '\v[\/](\.git|\.hg|\.idea|node_modules|tmp|)$',
    \ }
let g:ctrlp_prompt_mappings = {
    \ 'PrtSelectMove("j")':   ['<c-n>', '<c-j>', '<down>'],
    \ 'PrtSelectMove("k")':   ['<c-p>', '<c-k>', '<up>'],
    \ 'PrtHistory(-1)':       [],
    \ 'PrtHistory(1)':        [],
    \ }
if executable('ag')
    let g:ctrlp_user_command = 'ag --hidden -g "" --ignore ".git/" --ignore ".hg/" -l --nocolor %s'
    let g:ctrlp_use_caching = 0
endif
" }}}


" FZF settings {{{
let $FZF_DEFAULT_COMMAND = 'find . ! -wholename "*.DS_Store" ! -wholename "*.git*" ! -wholename "*.hg*" ! -wholename "*.idea*" ! -wholename "*node_modules*"'

if !empty($SSH_REMOTE)
    let $FZF_DEFAULT_COMMAND = "ssh " . $SSH_REMOTE . " 'cd /workspaces/" . $REPO_NAME . "; find . ! -wholename \"*.DS_Store\" ! -wholename \"*.git*\" ! -wholename \"*.hg*\" ! -wholename \"*.idea*\" ! -wholename \"*node_modules*\"'"
elseif executable('ag')
    let $FZF_DEFAULT_COMMAND = 'ag --hidden -g "" --ignore ".git/" --ignore ".hg/"'
endif
" }}}


" vim-airline settings {{{
let g:airline_theme = 'dark'
let g:airline#extensions#ale#enabled = 1
let g:airline#extensions#tabline#enabled = 1
" }}}


" ALE settings {{{
let g:ale_fix_on_save = 1
let g:ale_fixers = {
    \ 'typescript': ['tslint'],
    \ }
let g:ale_lint_on_insert_leave = 0
let g:ale_lint_on_text_changed = 'never'
let g:ale_linters = {
    \ 'html': [],
    \ 'javascript': ['standard'],
    \ 'ruby': ['rubocop', 'ruby'],
    \ }
let g:ale_set_highlights = 0
" }}}


" vim-flow settings {{{
let g:flow#enable = 0
" }}}


" ack settings {{{
if executable('ag')
    let g:ackprg = 'ag --vimgrep'
endif
" }}}


" vim-gitgutter settings {{{
let g:gitgutter_realtime = 0
" }}}


" vim-go settings {{{
let g:go_highlight_diagnostic_errors = 0
let g:go_highlight_diagnostic_warnings = 0
" }}}


" vim-jsx-pretty settings {{{
let g:vim_jsx_pretty_colorful_config = 1
" }}}

" vim-sneak settings {{{
let g:sneak#label = 1
" }}}

" vim-clojure-static settings {{{
let g:clojure_align_multiline_strings = 1
" }}}

" rainbow_parentheses settings {{{
let g:rainbow#pairs = [['(', ')'], ['[', ']'], ['{', '}']]

autocmd vimrc FileType lisp,clojure,scheme RainbowParentheses
" }}}

command CopyPath let @* = expand("%:p") | echo expand("%:p")
command EmDash :normal i—
command Markdown !pandoc --from markdown_strict --to html --output /tmp/markdown.html %
command PrettyJSON %!python -m json.tool
command Session :exe 'Obsession' projectroot#guess()
command Overmind :ProjectRootExe exe ':!overmind ' . input('Run: overmind ')
command! -nargs=* Psh :call <SID>RunProjectCommand([<f-args>])

fun! <SID>RunProjectCommand(args)
  call projectroot#exe(['exe', '":!' . join(a:args) . '"'])
endfun

fun! <SID>StripTrailingWhitespaces()
  if g:rosston_strip_trailing_whitespace == 1 && &filetype != 'markdown'
    let l = line(".")
    let c = col(".")
    %s/\s\+$//e
    call cursor(l, c)
  endif
endfun

fun! <SID>CountO()
  let l:count = v:count1
  while l:count
    call append(line('.'), '')
    normal! j
    let l:count -= 1
  endwhile
  execute "normal \"_cc\<Space>\<BS>\<Esc>"
  startinsert!
endfun

fun! <SID>CountShiftO()
  let l:count = v:count1
  while l:count
    call append(line('.') - 1, '')
    normal! k
    let l:count -= 1
  endwhile
  execute "normal \"_cc\<Space>\<BS>\<Esc>"
  startinsert!
endfun

nnoremap <silent> o :<C-U>call <SID>CountO()<CR>
nnoremap <silent> O :<C-U>call <SID>CountShiftO()<CR>
